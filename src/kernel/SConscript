from SCons.Environment import Environment
from SCons.Node.FS import Entry
from build_scripts.utility import glob_recursive, is_file_name, find_index
import os

TARGET_ENVIRONMENT: Environment = (
    Environment()
)  # will be cleared by Import() below, value only here to make type checker happy
Import("TARGET_ENVIRONMENT")

env = TARGET_ENVIRONMENT.Clone()
env.Append(
    LINKFLAGS=[
        "-Wl,-T",
        env.File("linker.ld").srcnode().path,  # type: ignore
        "-Wl,-Map=" + env.File("kernel.map").path,  # type: ignore
        "-pie",
    ],
    OBJPREFIX="kernel_",
    CPPDEFINES={"__KERNEL__": 1},
    CCFLAGS=["-fPIC"],
    CPATH=[env.Dir(".").srcnode(), env["SOURCE_DIRECTORY"]],  # type: ignore
    CPPPATH=[env.Dir(".").srcnode(), env["SOURCE_DIRECTORY"]],  # type: ignore
    ASFLAGS=["-I", env.Dir(".").srcnode(), "-I", env["SOURCE_DIRECTORY"], "-D", "__KERNEL__=1"],  # type: ignore
)

sources = (
    glob_recursive(env, "*.c")
    + glob_recursive(env, "*.asm")
    + glob_recursive(env, "*.c", env["LIBRARY_DIRECTORY"])
    + glob_recursive(env, "*.asm", env["LIBRARY_DIRECTORY"])
)
objects: list[Entry] = env.Object(sources)

kernel = env.Program("kernel.elf", objects)

Import("generate_font_header")
env.Requires(kernel, generate_font_header)
Export("kernel")
